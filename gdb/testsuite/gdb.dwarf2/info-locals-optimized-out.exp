# Copyright 2017-2025 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Test that "info locals" shows optimized out variables.

load_lib dwarf.exp

# This test can only be run on targets which support DWARF-2 and use gas.
require dwarf2_support

standard_testfile main.c -dw.S

# Make some DWARF for the test.

set asm_file [standard_output_file $srcfile2]
Dwarf::assemble $asm_file {
    global dwarf_regnum regname srcfile

    set buf_var [gdb_target_symbol buf]

    cu {} {
	DW_TAG_compile_unit {
		DW_AT_name $srcfile
		DW_AT_comp_dir /tmp
	} {
	    declare_labels int_type_label

	    # int
	    int_type_label: base_type {
		DW_AT_name "int"
		DW_AT_encoding @DW_ATE_signed
		DW_AT_byte_size 4 DW_FORM_sdata
	    }

	    DW_TAG_subprogram {
		MACRO_AT_func { main }
		DW_AT_external 1 flag
	    } {
		# A variable completely optimized out.
		DW_TAG_variable {
		    DW_AT_name "opt_out"
		    DW_AT_type :$int_type_label
		}
		DW_TAG_variable {
		    DW_AT_name const_bytes
		    DW_AT_type :$int_type_label
		    DW_AT_const_value "\x01\x01\x01\x01" DW_FORM_block1
		}
	    }
	}
    }
}

if { [prepare_for_testing ${testfile}.exp ${testfile} \
	  [list $srcfile $asm_file] {nodebug}] } {
    return -1
}

if {![runto_main]} {
    return -1
}

set lines [gdb_get_lines "info locals"]

# Make sure "info locals" shows optimized out variables.
gdb_assert \
    { [regexp [string_to_regexp "opt_out = <optimized out>"] $lines] } \
    "info locals shows optimized out variable"

# Regression test for PR cli/32525.
gdb_assert \
    { [regexp [string_to_regexp "const_bytes = 16843009"] $lines] } \
    "info locals shows DW_FORM_block1 variable"
